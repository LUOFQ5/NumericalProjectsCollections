C Copyright (c) 2014 David Patrick Hodapp
C
C Permission is hereby granted, free of charge, to any person obtaining a copy
C of this software and associated documentation files (the "Software"), to deal
C in the Software without restriction, including without limitation the rights   
C to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
C copies of the Software, and to permit persons to whom the Software is
C furnished to do so, subject to the following conditions:
C
C The above copyright notice and this permission notice shall be included in
C all copies or substantial portions of the Software.
C
C THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
C IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
C FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
C AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
C LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
C OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
C THE SOFTWARE.
C
      REAL(KIND=8) FUNCTION CONV_S_TO_K(S,A,W,L,B)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC     
C     PURPOSE:
C     CONVERT VERTICAL BENDING STRESS (S) TO EQUIVALENT STRESS INTENSITY
C     (K) FOR A M(T) SPECIMEN.  THE GIVEN STRESS IS USED TO DETERMINE
C     THE DISPLACEMENT ASSOCIATED WITH THE UN-CRACKED SPECIMEN (CONTANT 
C     STRAIN/DISPLACEMENT EXPERIMENTS).  THIS DISPLACEMENT IS THEN USED 
C     TO CALCULATE THE CORRESPONDING STRESS INTENSITY OF THE CRACKED 
C     SPECIMEN. 
C
C     PARAMETERS:
C     - S: VERTICAL BENDING STRESS [Pa]
C     - A: M(T) SPECIMEN HALF-CRACK LENGTH [m] (SEE ASTM E647-13)
C     - W: M(T) SPECIMEN DIMENSION W [m] (SEE ASTM E647-13)
C     - L: M(T) SPECIMEN DIMENSION L [m] (SEE ASTM E647-13)
C     - B: M(T) SPECIMEN DIMENSION B [m] (SEE ASTM E647-13)
C     OUTPUT:
C     - K: EQUIVALENT STRESS INTENSITY [MPa-m^(1/2)]
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
      IMPLICIT NONE
C
C     DECLARE CALLING PARAMETERS
      REAL(KIND=8), INTENT(IN)  :: S,A,W,L,B
C
C     DECLARE LOCAL VARIABLES
      REAL(KIND=8) :: PSYOUNGMOD,HALFDISP,AOVERB,V2AB,SEQ
      REAL(KIND=8), PARAMETER :: PI=3.1415926536
      REAL(KIND=8), PARAMETER :: YOUNGMOD=2.1E11
      REAL(KIND=8), PARAMETER :: POISSONRATIO=0.3
      PSYOUNGMOD=YOUNGMOD/(1.0-POISSONRATIO**2.0)
C     
C     DETERMINE HELF-DISPLACEMENT OF THE UNCRACKED SPECIMEN
      HALFDISP=0.5*(S/PSYOUNGMOD*2.0*L)
C     DETERMINE THE (EQUIVALENT) REMOTE STRESS IN THE CRACKED SPECIMEN
C     TO REALIZE THIS HALF-DISPLACEMENT.
C     **FORMULA FROM TADA ET AL. (2000, PART II)
      AOVERB=A/(W/2.0)
      V2AB=-1.071+0.250*AOVERB-0.357*AOVERB**2.0+0.121*AOVERB**3.0-
     1     0.047*AOVERB**4.0+0.008*AOVERB**5.0-
     2     1.071/AOVERB*LOG(1.0-AOVERB)
      SEQ=2.0*HALFDISP*(4.0*A/PSYOUNGMOD*V2AB+2.0*L/PSYOUNGMOD)**(-1.0)
C     DETERMINE THE CORRESPONDING STRESS INTENSITY
C     **FORMULA FROM ASTM E647-13 (EQ. A2.4)
      CONV_S_TO_K=SEQ*W*SQRT(PI*A/W**2.0*COS(PI*A/W)**(-1.0))/1.0E+06
C
      END FUNCTION