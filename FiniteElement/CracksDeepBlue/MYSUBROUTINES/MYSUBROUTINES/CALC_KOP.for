C Copyright (c) 2014 David Patrick Hodapp
C
C Permission is hereby granted, free of charge, to any person obtaining a copy
C of this software and associated documentation files (the "Software"), to deal
C in the Software without restriction, including without limitation the rights   
C to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
C copies of the Software, and to permit persons to whom the Software is
C furnished to do so, subject to the following conditions:
C
C The above copyright notice and this permission notice shall be included in
C all copies or substantial portions of the Software.
C
C THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
C IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
C FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
C AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
C LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
C OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
C THE SOFTWARE.
C
      REAL(KIND=8) FUNCTION CALC_KOP(SMIN,SMAX,A,W,L,B,
     1                               NUMINC,RECINCTIME,CRACKTIPCPRESS)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC     
C     PURPOSE:
C     CALCULATE KOP BASED ON RECORDED CRACK TIP CONTACT PRESSURES 
C     (RECORD 1511) OBTAINED USING USER SUBROUTINE URDFIL.
C
C     PARAMETERS:
C     - SMIN:           CYCLE MINIMUM STRESS (INCREMENT TIME=0.0) [Pa]
C     - SMAX:           CYCLE MAXIMUM STRESS (INCREMENT TIME=1.0) [Pa]
C     - A:              M(T) SPECIMEN HALF-CRACK LENGTH [m] (SEE ASTM E647-13)
C     - W:              M(T) SPECIMEN DIMENSION W [m] (SEE ASTM E647-13)
C     - L:              M(T) SPECIMEN DIMENSION L [m] (SEE ASTM E647-13)
C     - B:              M(T) SPECIMEN DIMENSION B [m] (SEE ASTM E647-13)
C     - NUMINC:         NUMBER OF INCREMENTS IN CURRENT STEP
C     - RECINCTIME:     INCREMENT TIME ASSOCIATED WITH CRACKTIPCPRESS 
C     - CRACKTIPCPRESS: CRACK TIP CONTACT PRESSURES (RECORD 1511)  
C     OUTPUT:
C     - KOP:            CURRENT (i) INCREMENT OPENING STRESS INTENSITY
C                       [MPa-m^(1/2)]
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
      IMPLICIT NONE
C
C     DECLARE CALLING PARAMETERS
      REAL(KIND=8), INTENT(IN) :: SMIN,SMAX,A,W,L,B
      INTEGER, INTENT(IN)      :: NUMINC
      REAL(KIND=8), DIMENSION(99), INTENT(IN) :: RECINCTIME,
     1                                           CRACKTIPCPRESS
C
C     DECLARE LOCAL VARIABLES
      INTEGER      :: I   
      REAL(KIND=8) :: KOPINCTIME,KMIN,KMAX
C
C     DECLARE FUNCTIONS TO BE CALLED BY SUBROUTINE
      REAL(KIND=8) :: CONV_S_TO_K 
C
      KMIN=CONV_S_TO_K(SMIN,A,W,L,B)
      KMAX=CONV_S_TO_K(SMAX,A,W,L,B)
C     IDENTIFIES CRACKS CLOSED FOR THE ENTIRE CYCLE
      IF ((CRACKTIPCPRESS(1).GT.0.0).AND.
     1    (CRACKTIPCPRESS(NUMINC).GT.0.0)) THEN
         KOPINCTIME=1.0
C     IDENTIFIES CRACKS OPEN FOR THE ENTIRE CYCLE
      ELSEIF ((CRACKTIPCPRESS(1).LE.0.0).AND.
     1        (CRACKTIPCPRESS(NUMINC).LE.0.0)) THEN
         KOPINCTIME=0.0
C     IDENTIFIES CRACKS WHICH OPEN DURING THE CYCLE 
      ELSE
         DO I=1,(NUMINC-1)
            IF ((CRACKTIPCPRESS(I).GE.0.0).AND.
     1          (CRACKTIPCPRESS(I+1).LT.0.0)) THEN
C              LINEARLY INTERPOLATE INCREMENT TIME OF KOP
               KOPINCTIME=RECINCTIME(I)-CRACKTIPCPRESS(I)*
     1                    (RECINCTIME(I+1)-RECINCTIME(I))/
     2                    (CRACKTIPCPRESS(I+1)-CRACKTIPCPRESS(I))
               GO TO 100	       
            END IF
         END DO   
      END IF        
C
 100  CONTINUE
C     DETERMINE THE CRACK OPENING LEVEL
      CALC_KOP=KMIN+KOPINCTIME*(KMAX-KMIN)
C
      END FUNCTION