C Copyright (c) 2014 David Patrick Hodapp
C
C Permission is hereby granted, free of charge, to any person obtaining a copy
C of this software and associated documentation files (the "Software"), to deal
C in the Software without restriction, including without limitation the rights   
C to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
C copies of the Software, and to permit persons to whom the Software is
C furnished to do so, subject to the following conditions:
C
C The above copyright notice and this permission notice shall be included in
C all copies or substantial portions of the Software.
C
C THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
C IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
C FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
C AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
C LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
C OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
C THE SOFTWARE.
C
      SUBROUTINE URDFIL(LSTOP,LOVRWRT,KSTEP,KINC,DTIME,TIME)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC     
C     (ABAQUS USER SUBROUTINE URDFIL)
C     PURPOSE:
C     READ RESULTS FILE (*CONTACT FILE DATA) AND PASS DATA TO ABAQUS 
C     USER SUBROUTINE UEXTERNALDB THROUGH A COMMON BLOCK.
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      INCLUDE 'ABA_PARAM.INC'
C
      DIMENSION ARRAY(513),JRRAY(NPRECD,513),TIME(2)
      EQUIVALENCE (ARRAY(1),JRRAY(1,1))
C
C     DECLARE LOCAL VARIABLES
      INTEGER                       :: I,WRITEINDEX
      INTEGER, DIMENSION(9999)      :: NODENUMBERS
      REAL(KIND=8), DIMENSION(9999) :: CONTACTPRESSURES
C     **ARRAYS ARE SIZED ASSUMING THAT NO MORE THAN 9999 NODES WILL COMPRISE
C       THE CONTACT PAIR SURFACE
C
C     COMMON BLOCK DATA
C     - RECSTEP:        CURRENT ANALYSIS STEP
C     - RECINC:         CURRENT ANALYSIS INCREMENT (WITHIN CURRENT STEP)
C     - DEBONDFLAG:     FLAG=1 DURING DEBOND STEP, FLAG=0 OTHERWISE   
C     - CRACKTIPNODE:   CRACK TIP NODE LABEL (RECORD 1993/ATTRIBUTE 5)
C     - DEBONDCRACKLEN: CUMULATIVE DEBOND CRACK LENGTH 
C                       (RECORD 1993/ATTRIBUTE 7)
C     - RECINCTIME:     TIME INDEX E [0,1] OF CURRENT ANALYSIS INCREMENT
C     - CRACKTIPCPRESS: CSTRESS CONTACT PRESSURES (RECORD 1511/ATTRIBUTE 1)
C                       OF CRACKTIPNODE
C       **ZEROTH INCREMENT ADDED INDEPENDENT OF RESULTS FILE AT INDEX=1
C         ACTUAL ARRAY WILL HAVE RECINC+1 ENTRIES
      INTEGER      :: RECSTEP,RECINC,DEBONDFLAG,CRACKTIPNODE
      REAL(KIND=8) :: DEBONDCRACKLEN 
      REAL(KIND=8), DIMENSION(99) :: RECINCTIME,CRACKTIPCPRESS
      COMMON /KDATA1/ RECSTEP,RECINC,DEBONDFLAG,CRACKTIPNODE,
     1                DEBONDCRACKLEN,RECINCTIME,CRACKTIPCPRESS
C
C     GO TO START OF CURRENT INCREMENT IN RESULTS FILE
      CALL POSFIL(KSTEP,KINC,ARRAY,JRCD)	  
C
C     SPECIFY INITIAL VALUES FOR INTERNAL VARIABLES WHICH ARE 
C     REWRITTEN FOR EVERY SUBROUTINE CALL
      WRITEINDEX=1
      DEBONDFLAG=0
      CRACKTIPNODE=-1
C     **RECORD 1993/ATTRIBUTE 5 NOT DEFINED UNTIL FIRST DEBOND STEP
C
C     READ RESULTS FILE RECORDS SEQUENTIALLY
      DO I=1,999999999	  
         CALL DBFILE(0,ARRAY,JRCD)
         IF (JRCD.NE.0) GO TO 100
         KEY=JRRAY(1,2)
C
C        READ IN CRACKTIPNODE (RECORD 1993/ATTRIBUTE 5)
C        READ IN DEBONDCRACKLEN (RECORD 1993/ATTRIBUTE 7)
         IF (KEY.EQ.1993) THEN
            CRACKTIPNODE=JRRAY(1,7)
C           IDENTIFIES DEBOND STEP
            IF (ARRAY(9).GT.0) THEN
               DEBONDFLAG=1
               DEBONDCRACKLEN=DBLE(ARRAY(9))
            END IF
         END IF
C
C       READ IN NODE LABELS (RECORD 1504/ATTRIBUTE 1)
C       READ IN CSTRESS CONTACT PRESSURES (RECORD 1511/ATTRIBUTE 1)
C       **THESE TWO RECORDS ARE SEQUENTIAL 
         IF (KEY.EQ.1504) THEN
            NODENUMBERS(WRITEINDEX)=JRRAY(1,3)
         END IF
         IF (KEY.EQ.1511) THEN	    
            CONTACTPRESSURES(WRITEINDEX)=DBLE(ARRAY(3))
            WRITEINDEX=WRITEINDEX+1
         END IF
      END DO
C
 100  CONTINUE      
C
C     EXIT SUBROUTINE IF NO IDENTIFIED CRACK TIP NODE
      IF (CRACKTIPNODE.EQ.-1) GO TO 200    
C
C     DETERMINE CSTRESS CONTACT PRESSURE (RECORD 1511/ATTRIBUTE 1) FOR 
C     CRACKTIPNODE AND SAVE DATA TO COMMON BLOCK
      DO I=1,(WRITEINDEX-1)
         IF (NODENUMBERS(I).EQ.CRACKTIPNODE) THEN
C           IDENTIFIES THE FIRST INCREMENT OF A STEP AND RECORDS
C           END OF PREVIOUS STEP DATA AS THE ZEROTH INCREMENT 
            IF (KINC.EQ.1) THEN
               RECINCTIME(1)=0.0
               CRACKTIPCPRESS(1)=CRACKTIPCPRESS(RECINC+1)
            END IF
            RECSTEP=KSTEP
            RECINC=KINC
            RECINCTIME(RECINC+1)=DBLE(TIME(1))
            CRACKTIPCPRESS(RECINC+1)=DBLE(CONTACTPRESSURES(I))
            GO TO 200
         END IF
      END DO
C
 200  CONTINUE
      RETURN
      END SUBROUTINE